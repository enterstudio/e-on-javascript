# Copyright 2009 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

# This is new code to E-on-JS.

def makeTextWriter {
  to makeBufferingPair() {
    def strings := [].diverge()
    def buffer {
      to snapshot() {
        return "".rjoin(strings.snapshot())
      }
    }
    def textWriter {
      to write(text :String) {
        strings.push(text)
      }
      to quote(x) {
        textWriter.print(x)
      }
      to print(x) {
        def state := Ref.state(x)
        if (state == "NEAR") {
          x.__printOn(textWriter)
        } else if (state == "BROKEN") {
          textWriter.write("<ref broken by ")
          textWriter.print(Ref.optProblem(x))
          textWriter.write(">")
        } else { # state == "EVENTUAL"
          if (Ref.isResolved(x)) {
            textWriter.write("<Far ref>")
          } else {
            textWriter.write("<Promise>")
          }
        }
      }
    }
    return [textWriter, buffer]
  }
}